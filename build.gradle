buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'ivy-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.jcraft:jsch:0.1.51'
	compile 'com.jcraft:jsch.agentproxy.connector-factory:0.0.7'
	compile 'com.jcraft:jsch.agentproxy.jsch:0.0.7'
    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8', 'cglib:cglib-nodep:2.2.2'
    testCompile 'org.apache.sshd:sshd-core:0.9.0'
}

sourceSets.all {
    // Exclude old JSch to avoid test with it.
    final bundled = ['jsch-0.1.46.jar']
    compileClasspath -= compileClasspath.filter { it.name in bundled }
    runtimeClasspath -= runtimeClasspath.filter { it.name in bundled }
}

test {
    useJUnit {
        excludeCategories 'org.hidetake.gradle.ssh.server.ServerIntegrationTest'
    }
}

task serverIntegrationTest(type: Test) {
    useJUnit {
        includeCategories 'org.hidetake.gradle.ssh.server.ServerIntegrationTest'
    }
    reports {
        html.destination = file("$buildDir/reports/server-integration-tests")
    }
}

jacoco {
    toolVersion = '0.7.1.201405082137'
}

task jacocoServerIntegrationTestReport(type: JacocoReport) {
    executionData serverIntegrationTest
    sourceSets sourceSets.main
    reports.html.destination "$buildDir/reports/jacoco/server-integration-tests"
}

check.dependsOn serverIntegrationTest
check.dependsOn jacocoTestReport
check.dependsOn jacocoServerIntegrationTestReport

if (System.getProperty('os.name') == 'Linux') {
    tasks.withType(JavaForkOptions) {
        systemProperty 'java.security.egd', 'file:/dev/./urandom'
    }
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    repositories {
        ivy {
            name 'local'
            url "${System.properties['user.home']}/.ivy2/local"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
        ivyJava(IvyPublication) {
            from components.java
        }
    }
}

if ('bintrayUpload' in gradle.startParameter.taskNames) {
    project.version -= '-SNAPSHOT'
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'gradle-ssh-plugin'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'ssh', 'deploy']
        version {
            attributes = ['gradle-plugin': 'org.hidetake.ssh:org.hidetake:gradle-ssh-plugin']
        }
    }
}

task install(dependsOn: 'publishIvyJavaPublicationToLocalRepository') {
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

task archiveDependencies(type: Zip, dependsOn: jar) {
    classifier = 'all'
    final excludeLibs = ['jsch-0.1.46.jar']
    final runtimeLibs = configurations.runtime - configurations.runtime.filter { it.name in excludeLibs }
    into('dep-libs') {
        from runtimeLibs
        from 'build/libs'
    }
}
